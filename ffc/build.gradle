/*
 * Copyright (c) 2018 NECTEC
 *   National Electronics and Computer Technology Center, Thailand
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *    http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: "org.jlleitschuh.gradle.ktlint"

android {
    compileSdkVersion 28

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
        androidTest.java.srcDirs += 'src/androidTest/kotlin'
    }

    defaultConfig {
        applicationId "ffc.app"
        minSdkVersion 19
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        debug {
            storeFile file("debug.keystore")
        }

        Properties properties
        if (rootProject.file('local.properties').exists()) {
            properties = new Properties()
            properties.load(rootProject.file('local.properties').newDataInputStream())
        }
        release {
            storeFile file("release.keystore")
            storePassword properties?.getProperty("release.storepassword") ?:
                    System.getenv('RELEASE_STORE_PASSWORD') ?: ""
            keyAlias 'ffcrelease'
            keyPassword properties?.getProperty("release.keypassword") ?:
                    System.getenv('RELEASE_KEY_PASSWORD') ?: ""
        }
    }

    buildTypes {
        debug {
            signingConfig signingConfigs.debug
        }
        release {
            signingConfig signingConfigs.release
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    packagingOptions {
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
    }
}

// from https://github.com/android10/Android-KotlinInTests/blob/master/app/build.gradle
// Ensure Kotlin will not be used in production code.
afterEvaluate {
    android.sourceSets.all { sourceSet ->
        if (!sourceSet.name.startsWith('test') || !sourceSet.name.startsWith('androidTest')) {
            sourceSet.kotlin.setSrcDirs([])
        }
    }
}

dependencies {
    def appcompatVersion = '27.1.1'
    def firebaseVersion = '16.0.3'
    def playVersion = '15.0.1'
    def retrofitVersion = '2.4.0'
    def ankoVersion = '0.10.5'

    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'android.arch.lifecycle:extensions:1.1.1'
    //noinspection GradleCompatible
    implementation "com.android.support:appcompat-v7:$appcompatVersion"
    //noinspection GradleCompatible
    implementation "com.android.support:design:$appcompatVersion"
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    implementation "com.android.support:support-annotations:$appcompatVersion"
    implementation 'com.fatboyindustrial.gson-jodatime-serialisers:gson-jodatime-serialisers:1.6.0'
    implementation 'com.github.bumptech.glide:glide:4.7.1'
    implementation 'com.github.ffc-nectec:entities:1.0.0-alpha6'
    implementation 'com.github.piruin.geok:geok-gson:1.0.0-alpha-2'
    implementation 'com.github.piruin:retrofit-dsl:1.2'
    implementation "com.google.android.gms:play-services-maps:$playVersion"
    implementation 'com.google.code.gson:gson:2.8.2'
    implementation "com.google.firebase:firebase-core:$firebaseVersion"
    implementation 'com.google.firebase:firebase-messaging:17.3.1'
    implementation 'com.google.maps.android:android-maps-utils:0.5'
    implementation "com.squareup.retrofit2:converter-gson:$retrofitVersion"
    implementation "com.squareup.retrofit2:retrofit:$retrofitVersion"
    implementation 'joda-time:joda-time:2.10'
    implementation 'jp.wasabeef:glide-transformations:3.3.0'
    implementation 'me.piruin:spinney:0.2.2'
    implementation "org.jetbrains.anko:anko-appcompat-v7-commons:$ankoVersion"
    implementation "org.jetbrains.anko:anko-design:$ankoVersion"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "th.or.nectec.android:marlo:0.5.3" exclude group: 'com.android.support'
    implementation "th.or.nectec.android:thai-widget:0.6.2" exclude group: 'com.android.support'

    testImplementation 'junit:junit:4.12'
    testImplementation 'org.amshove.kluent:kluent:1.41'

    androidTestImplementation 'org.amshove.kluent:kluent-android:1.40'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test:rules:1.0.2'

    annotationProcessor 'com.github.bumptech.glide:compiler:4.7.1'
}

apply plugin: 'com.google.gms.google-services'
